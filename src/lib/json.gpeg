TOP              <- JSON

UTF8CHAR         <- { . }

SPACE            <- %s+
MULTILINECOMMENT <- '/*' ( !'*/' .)* '*/'
COMMENT          <- '//' [^\n]* '\n'
__prefix         <- ( MULTILINECOMMENT / COMMENT / SPACE )*
END              <- !.

JSON             <- VALUE END
HASH             <- { CBOPEN OPTHASHELTS CBCLOSE }
OPTHASHELTS      <- HASHELTS ?
HASHELTS         <- HASHELT ( COMMA HASHELT )*
HASHELT          <- { NAME COLON VALUE }
ARRAY            <- { ABOPEN OPTARRAYELTS ABCLOSE }
OPTARRAYELTS     <- ARRAYELTS ?
ARRAYELTS        <- VALUE ( COMMA VALUE )*
VALUE            <- BIGSTRING / STRING / FLOAT / INT / BOOL / NULL / HASH / ARRAY
BIGSTRING        <- '"""' { ( !'"""' UTF8CHAR )* } '"""'
NAME             <- STRING
STRING           <- '"' { ( '\\' { [nrtbf"\\] / 'u' [a-fA-F0-9]^4 } / !'"' UTF8CHAR )* } '"'
INT              <- { '-'? [0-9]+ }
FLOAT            <- { '-'? [0-9]* '.' [0-9]+ }
BOOL             <- { 'true' / 'false' }
NULL             <- { 'null' }
CBOPEN           <- '{'
CBCLOSE          <- '}'
ABOPEN           <- '['
ABCLOSE          <- ']'
COMMA            <- ','
COLON            <- ':'

--[[
UTF8CHAR         <- { ASCII / DOUBLE / TRIPLE / QUADRUPLE }
ASCII            <- [0x00-0x7f]
DOUBLE           <- [0xc0-0xdf] [0x80-0xbf]
TRIPLE           <- [0xe0-0xef] [0x80-0xbf] [0x80-0xbf]
QUADRUPLE        <- [0xf0-0xf7] [0x80-0xbf] [0x80-0xbf] [0x80-0xbf]
]]

